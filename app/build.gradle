apply plugin: 'com.android.application'

apply plugin: 'kotlin-android'

apply plugin: 'kotlin-android-extensions'

def versionMajor = 3
def versionMinor = 0
def versionPatch = 0
def versionBuild = 0

def props = new Properties()
props.load(new FileInputStream('app/keys.properties'))

android {
    compileSdkVersion 27
    buildToolsVersion '27.0.3'

    defaultConfig {
        minSdkVersion 16
        targetSdkVersion 27

        applicationId 'com.njlabs.showjava'
        versionCode versionMajor * 10000 + versionMinor * 1000 + versionPatch * 100 + versionBuild
        versionName "${versionMajor}.${versionMinor}.${versionPatch}"

        testInstrumentationRunner 'android.support.test.runner.AndroidJUnitRunner'
        multiDexEnabled true

        resConfigs "en"
        vectorDrawables.useSupportLibrary true

        proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'

        resValue 'string', 'appName', 'Show Java'
        resValue 'string', 'bugReportUri', 'Show Java'
        resValue 'string', 'originalApplicationId', 'com.njlabs.showjava'
        resValue 'string', 'deviceId', props.getProperty('deviceId')
        resValue 'string', 'adUnitId', props.getProperty('adUnitId')
    }

    testOptions {
        unitTests {
            includeAndroidResources true
        }
    }

    dexOptions {
        jumboMode true
        javaMaxHeapSize '4g'
    }

    packagingOptions {
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/NOTICE.txt'
        exclude 'META-INF/MANIFEST.MF'
        exclude 'META-INF/rxjava.properties'
    }

    lintOptions {
        abortOnError false
    }

    flavorDimensions 'type'

    productFlavors {
        standard {
            buildConfigField 'boolean', 'IS_TRAVIS', 'false'
            dimension = 'type'
        }

        travis {
            buildConfigField 'boolean', 'IS_TRAVIS', 'true'
            dimension = 'type'
        }
    }

    buildTypes {
        release {
            minifyEnabled true
            shrinkResources true
        }
        debug {
            minifyEnabled false
            shrinkResources false
        }
    }

    sourceSets {
        main.java.srcDirs += 'src/main/kotlin'
        test.java.srcDirs += 'src/test/kotlin'
        androidTest.java.srcDirs += 'src/androidTest/kotlin'
    }
}

ext.support_version = '27.0.2'
ext.firebase_version = '11.8.0'

dependencies {
    // MULTI-DEX SUPPORT FOR PRE LOLLIPOP
    implementation 'com.android.support:multidex:1.0.2'
    // KOTLIN STANDARD LIBRARY
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jre7:$kotlin_version"
    // FIREBASE LIBRARIES
    implementation "com.google.firebase:firebase-core:$firebase_version"
    implementation "com.google.firebase:firebase-crash:$firebase_version"
    implementation "com.google.firebase:firebase-ads:$firebase_version"
    // ANDROID SUPPORT LIBRARIES
    implementation "com.android.support:appcompat-v7:$support_version"
    implementation "com.android.support:design:$support_version"
    implementation "com.android.support:cardview-v7:$support_version"
    implementation "com.android.support:recyclerview-v7:$support_version"
    implementation 'com.android.support.constraint:constraint-layout:1.1.0-beta4'
    // APACHE COMMONS LIBRARIES
    implementation 'commons-io:commons-io:2.6'
    implementation 'commons-cli:commons-cli:1.4'
    // DEX2JAR AND FRIENDS
    implementation 'asm:asm-all:3.3.1'
    implementation files('libs/smali-2.2.1.jar')
    implementation files('libs/dex-ir-1.12.jar')
    implementation files('libs/dex-reader-1.15.jar')
    implementation files('libs/dex-tools-0.0.9.15.jar')
    implementation files('libs/dex-translator-0.0.9.15.jar')
    // CLASS FILE READER - JAVA DECOMPILER
    implementation files('libs/cfr_0_117.jar')
    // DEPENDENCIES FOR JaDX
    implementation files('libs/dx-1.10.jar')
    implementation files('libs/android-5.1-clst-core.jar')
    implementation 'org.slf4j:slf4j-api:1.7.21'
    implementation 'uk.com.robust-it:cloning:1.9.2'
    // APK PARSER AND BINARY XML DECODER
    implementation 'net.dongliu:apk-parser:2.2.0'
    // FERN FLOWER
    implementation files('libs/fernflower.jar')
    // RxJAVA AND FRIENDS
    implementation 'io.reactivex.rxjava2:rxandroid:2.0.1'
    implementation 'io.reactivex.rxjava2:rxkotlin:2.1.0'
    implementation 'com.jakewharton.rxrelay2:rxrelay:2.0.0'
    implementation 'uk.co.chrisjenx:calligraphy:2.3.0'
    implementation 'com.jakewharton.timber:timber:4.5.1'
    implementation('org.solovyev.android:checkout:1.0.1', {
        exclude group: 'com.google.code.findbugs', module: 'jsr305'
    })
    implementation 'pub.devrel:easypermissions:0.4.3'
    implementation('org.solovyev.android:checkout:1.2.1', {
        exclude group: 'com.google.code.findbugs', module: 'jsr305'
    })
    implementation 'com.nononsenseapps:filepicker:4.1.0'
    implementation 'io.github.yavski:fab-speed-dial:1.0.6'
    implementation 'com.daimajia.numberprogressbar:library:1.4@aar'
    implementation 'com.davemorrissey.labs:subsampling-scale-image-view:3.6.0'
    // LEAKCANARY AND FRIENDS
    debugImplementation 'com.squareup.leakcanary:leakcanary-android:1.5.1'
    releaseImplementation 'com.squareup.leakcanary:leakcanary-android-no-op:1.5.1'
    testImplementation 'com.squareup.leakcanary:leakcanary-android-no-op:1.5.1'
    testImplementation 'junit:junit:4.12'
    androidTestImplementation('com.android.support.test.espresso:espresso-core:2.2.2', {
        exclude group: 'com.android.support', module: 'support-annotations'
        exclude group: 'com.google.code.findbugs', module: 'jsr305'
    })
}

apply plugin: 'com.google.gms.google-services'