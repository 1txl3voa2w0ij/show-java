apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'io.fabric'

def versionMajor = 3
def versionMinor = 0
def versionPatch = 1
def versionBuild = 0

def gitSha = 'git rev-parse --short HEAD'.execute([], project.rootDir).text.trim()
def buildTime = new Date().format("yyyy-MM-dd'T'HH:mm'Z'", TimeZone.getTimeZone('UTC'))

def isCi = 'true' == System.getenv('CI')
def preDexEnabled = 'true' == System.getProperty('PRE_DEX', 'true')

def props = new Properties()
try {
    props.load(new FileInputStream('app/keys.properties'))
} catch (e) {
    props.load(new FileInputStream('app/keys.properties.example'))
    println(e.message)
}

android {
    compileSdkVersion 28
    buildToolsVersion '28.0.3'

    dexOptions {
        preDexLibraries = preDexEnabled && !isCi
    }

    defaultConfig {
        minSdkVersion 16
        targetSdkVersion 28

        applicationId 'com.njlabs.showjava'
        versionCode versionMajor * 10000 + versionMinor * 1000 + versionPatch * 100 + versionBuild
        versionName "${versionMajor}.${versionMinor}.${versionPatch}"

        testInstrumentationRunner 'androidx.test.runner.AndroidJUnitRunner'

        resConfigs "en"
        vectorDrawables.useSupportLibrary true

        proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'

        resValue 'string', 'appName', 'Show Java'
        resValue 'string', 'bugReportUri', 'https://github.com/niranjan94/show-java/issues'
        resValue 'string', 'originalApplicationId', 'com.njlabs.showjava'
        resValue 'string', 'deviceId', props.getProperty('deviceId')
        resValue 'string', 'adUnitId', props.getProperty('adUnitId')
        resValue 'string', 'admobAppId', props.getProperty('admobAppId')

        buildConfigField 'String', 'GIT_SHA', "\"${gitSha}\""
        buildConfigField 'String', 'BUILD_TIME', "\"${buildTime}\""
        buildConfigField 'Boolean', 'IS_CI', "${isCi}"

        manifestPlaceholders = [admobAppId: props.getProperty('admobAppId')]
    }

    testOptions {
        unitTests {
            includeAndroidResources true
        }
    }

    dexOptions {
        javaMaxHeapSize '4g'
    }

    packagingOptions {
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/NOTICE.txt'
        exclude 'META-INF/MANIFEST.MF'
        exclude 'META-INF/rxjava.properties'
    }

    lintOptions {
        abortOnError false
    }

    flavorDimensions 'type'

    productFlavors {
        standard {
            dimension = 'type'
        }
    }

    buildTypes {
        release {
            minifyEnabled true
            shrinkResources true
        }
        debug {
            minifyEnabled false
            shrinkResources false
        }
    }

    sourceSets {
        main.java.srcDirs += 'src/main/kotlin/'
        test.java.srcDirs += 'src/test/kotlin/'
        androidTest.java.srcDirs += 'src/androidTest/kotlin/'
    }
}

def androidxVersion = '1.0.0'
def workVersion = '1.0.0-alpha10'

dependencies {
    // KOTLIN STANDARD LIBRARY
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"

    // FIREBASE LIBRARIES
    implementation 'com.google.firebase:firebase-core:16.0.4'
    implementation 'com.google.firebase:firebase-ads:17.0.0'
    implementation 'com.crashlytics.sdk.android:crashlytics:2.9.5'

    // ANDROID SUPPORT LIBRARIES
    implementation "androidx.appcompat:appcompat:$androidxVersion"
    implementation "com.google.android.material:material:$androidxVersion"
    implementation "androidx.cardview:cardview:$androidxVersion"
    implementation "androidx.legacy:legacy-support-v4:$androidxVersion"
    implementation "androidx.recyclerview:recyclerview:$androidxVersion"
    implementation "androidx.legacy:legacy-support-core-utils:$androidxVersion"
    implementation "androidx.core:core:$androidxVersion"
    implementation 'androidx.constraintlayout:constraintlayout:2.0.0-alpha2'

    implementation("android.arch.work:work-runtime-ktx:$workVersion", {
        exclude group: 'com.google.guava', module: 'listenablefuture'
    })
    implementation("android.arch.work:work-firebase:$workVersion", {
        exclude group: 'com.google.guava', module: 'listenablefuture'
    })

    // APACHE COMMONS LIBRARIES
    implementation 'commons-io:commons-io:2.6'
    implementation 'commons-cli:commons-cli:1.4'

    // DEX2JAR AND FRIENDS
    implementation 'asm:asm-all:3.3.1'
    implementation files('libs/smali-2.2.1.jar')
    implementation files('libs/dex-ir-1.12.jar')
    implementation files('libs/dex-reader-1.15.jar')
    implementation files('libs/dex-tools-0.0.9.15.jar')
    implementation files('libs/dex-translator-0.0.9.15.jar')

    // CLASS FILE READER - JAVA DECOMPILER
    implementation files('libs/cfr_0_117.jar')

    // DEPENDENCIES FOR JaDX
    implementation files('libs/dx-1.10.jar')
    implementation files('libs/android-5.1.jar')
    implementation files('libs/jadx-core-0.6.1.jar')
    implementation 'org.slf4j:slf4j-api:1.7.25'
    implementation 'uk.com.robust-it:cloning:1.9.10'

    // APK PARSER AND BINARY XML DECODER
    implementation 'net.dongliu:apk-parser:2.6.4'

    // FERN FLOWER
    implementation files('libs/fernflower.jar')

    // RxJAVA AND FRIENDS
    implementation 'io.reactivex.rxjava2:rxandroid:2.1.0'
    implementation 'io.reactivex.rxjava2:rxkotlin:2.3.0'
    implementation 'com.jakewharton.rxrelay2:rxrelay:2.1.0'

    // LEAKCANARY AND FRIENDS
    testImplementation 'junit:junit:4.12'
    androidTestImplementation('androidx.test.espresso:espresso-core:3.1.0-beta02', {
        exclude group: 'com.android.support', module: 'support-annotations'
        exclude group: 'com.google.code.findbugs', module: 'jsr305'
    })

    // EVERYBODY ELSE
    implementation 'io.github.inflationx:calligraphy3:3.0.0'
    implementation 'io.github.inflationx:viewpump:1.0.0'
    implementation 'com.jakewharton.timber:timber:4.7.1'
    implementation('org.solovyev.android:checkout:1.0.1', {
        exclude group: 'com.google.code.findbugs', module: 'jsr305'
    })
    implementation 'pub.devrel:easypermissions:2.0.0'
    implementation('org.solovyev.android:checkout:1.2.1', {
        exclude group: 'com.google.code.findbugs', module: 'jsr305'
    })
    implementation 'com.github.javiersantos:PiracyChecker:1.2.4'
    implementation 'com.github.kobakei:MaterialFabSpeedDial:1.2.0'
    implementation 'com.daimajia.numberprogressbar:library:1.4@aar'
    implementation 'com.davemorrissey.labs:subsampling-scale-image-view:3.10.0'
    implementation 'com.github.angads25:filepicker:1.1.1'

}

apply plugin: 'com.google.gms.google-services'